plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
    id 'java'
}

group = 'net.foulest'
version = '1.3.3'
description = 'KitPvP'

compileJava.options.encoding = 'UTF-8'
java.sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    // Maven repositories
    mavenCentral()
    mavenLocal()

    // Local libraries
    flatDir {
        dirs 'libs'
    }

    maven {
        url 'https://repo.minebench.de'
        name 'Minebench'
    }
}

dependencies {
    // Spigot 1.8.8 - necessary for project; provided in libs
    // https://hub.spigotmc.org/nexus/content/repositories/snapshots
    compileOnly group: 'org.spigotmc', name: 'spigot', version: '1.8.8-R0.1-SNAPSHOT'

    // MariaDB Java Client - for database connections
    // https://mvnrepository.com/artifact/org.mariadb.jdbc/mariadb-java-client
    implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '3.3.2'

    // SQLite JDBC - for database connections
    // https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.45.1.0'

    // Apache Commons DBCP2 - for database pooling
    // https://mvnrepository.com/artifact/org.apache.commons/commons-dbcp2
    implementation group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.11.0'

    // PlaceholderAPI - for placeholders
    // https://mvnrepository.com/artifact/me.clip/placeholderapi
    compileOnly group: 'me.clip', name: 'placeholderapi', version: '2.11.1'

    // WorldEdit - required for WorldGuard
    // https://mvnrepository.com/artifact/com.sk89q.worldedit/worldedit-bukkit
    compileOnly group: 'com.sk89q.worldedit', name: 'worldedit-bukkit', version: '6.1.9'

    // WorldGuard - for region data
    // https://mvnrepository.com/artifact/com.sk89q.worldguard/worldguard-legacy
    compileOnly group: 'com.sk89q.worldguard', name: 'worldguard', version: '6.1'

    // Lombok - for reducing boilerplate code
    // https://projectlombok.org
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.30'
}

tasks {
    shadowJar {
        archiveFileName.set("${project.name}-${project.version}.jar")
    }

    compileJava {
        dependsOn(clean)
    }

    build {
        dependsOn(shadowJar)
    }

    processResources {
        filesMatching('**/*.yml') {
            filter { line ->
                line.replace('${project.version}', project.version)
            }
        }
    }

    tasks.register('sourceJar', Jar) {
        from sourceSets.main.allJava
    }

    jar.configure {
        archiveClassifier = 'default'
    }

    shadowJar.configure {
        archiveClassifier = null
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "net.foulest.vulture"
            artifactId = project.name
            version = project.version

            from components.java
        }
    }
}
