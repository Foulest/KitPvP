plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
    id 'java'
}

group = 'net.foulest'
version = '1.4.5'
description = 'KitPvP'

// Set the project's language level
java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

repositories {
    // Maven repositories
    mavenCentral()
    mavenLocal()

    // Local libraries
    flatDir {
        dirs 'libs'
    }

    maven {
        name 'Minebench'
        url 'https://repo.minebench.de'
    }
}

dependencies {
    // Spigot 1.8.8 - necessary for project; provided in libs
    // https://hub.spigotmc.org/nexus/content/repositories/snapshots
    compileOnly group: 'org.spigotmc', name: 'spigot', version: '1.8.8-R0.1-SNAPSHOT'

    // HikariCP - for database pooling
    // https://mvnrepository.com/artifact/com.zaxxer/HikariCP
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '4.0.3'

    // PlaceholderAPI - for placeholders
    // https://mvnrepository.com/artifact/me.clip/placeholderapi
    compileOnly group: 'me.clip', name: 'placeholderapi', version: '2.11.1'

    // WorldEdit - required for WorldGuard
    // https://mvnrepository.com/artifact/com.sk89q.worldedit/worldedit-bukkit
    compileOnly group: 'com.sk89q.worldedit', name: 'worldedit-bukkit', version: '6.1.9'

    // WorldGuard - for region data
    // https://mvnrepository.com/artifact/com.sk89q.worldguard/worldguard-legacy
    compileOnly group: 'com.sk89q.worldguard', name: 'worldguard', version: '6.1'

    // JetBrains Annotations - for code inspection and documentation
    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '26.0.2'

    // Lombok - for reducing boilerplate code
    // https://projectlombok.org
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.36'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.36'
}

tasks {
    jar {
        archiveFileName.set("${project.name}-${project.version}-default.jar")
    }

    shadowJar {
        archiveFileName.set("${project.name}-${project.version}.jar")
        enableRelocation = true
        relocationPrefix = 'net.foulest.kitpvp.shaded'
    }

    compileJava {
        dependsOn(clean)
        options.encoding = 'UTF-8'
    }

    build {
        dependsOn(shadowJar)
    }

    processResources {
        filesMatching('**/*.yml') {
            filter { final line ->
                line.replace('${project.version}', project.version)
            }
        }
    }

    tasks.register('sourceJar', Jar) {
        from sourceSets.main.allJava
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'net.foulest.kitpvp'
            artifactId = project.name
            version = project.version

            from components.java
        }
    }
}
